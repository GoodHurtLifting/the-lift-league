pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    plugins {
        id("com.android.application")      version "8.10.1"
        id("com.android.library")          version "8.10.1"
        id("org.jetbrains.kotlin.android") version "2.1.21"

        id("com.google.gms.google-services")   version "4.4.2"
        id("com.google.firebase.crashlytics")  version "3.0.4"
    }

    // ---- Flutter ----
    def flutterSdkPath = {
        def props = new Properties()
        def localProps = file("local.properties")
        if (localProps.exists()) {
            localProps.withInputStream { props.load(it) }
        } else {
            def envPath = System.getenv("FLUTTER_ROOT")
            if (envPath != null) props.setProperty("flutter.sdk", envPath)
        }
        def path = props.getProperty("flutter.sdk")
        assert path != null : "flutter.sdk not set in local.properties or FLUTTER_ROOT"
        return path
    }()
    includeBuild("${flutterSdkPath}/packages/flutter_tools/gradle")
}

// Flutter loader (no version conflicts here)
plugins {
    id("dev.flutter.flutter-plugin-loader") version "1.0.0"
}

include(":app")
