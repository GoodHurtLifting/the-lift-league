
pluginManagement {
    resolutionStrategy {
        eachPlugin { details ->
            if (details.requested.id.id == "com.android.application"
                    || details.requested.id.id == "com.android.library") {
                useModule("com.android.tools.build:gradle:8.7.3")
            }
            if (details.requested.id.id == "org.jetbrains.kotlin.android") {
                useModule("org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.0")
            }
        }
    }

    def flutterSdkPath = {
        def properties = new Properties()
        def localProps = file("local.properties")
        if (localProps.exists()) {
            localProps.withInputStream { properties.load(it) }
        } else {
            def envPath = System.getenv("FLUTTER_ROOT")
            if (envPath != null) {
                properties.setProperty("flutter.sdk", envPath)
            }
        }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties or FLUTTER_ROOT"
        return flutterSdkPath
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.7.3" apply false
    // START: FlutterFire Configuration
    id "com.google.gms.google-services" version "4.4.2" apply false
    // END: FlutterFire Configuration
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
}

include ":app"